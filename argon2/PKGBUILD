pkgname=argon2
_gitname="phc-winner-argon2"
pkgdesc="The password hash Argon2, winner of PHC"
pkgver=20190702.r27.gf57e61e
pkgrel=1
arch=('i686' 'x86_64')
conflicts=("argon2")
provides=("argon2")
url="https://github.com/AcreetionOS-Linux/argon2"
license=('custom:CC-0')
depends=('glibc')
makedepends=('git')
source=('git+https://github.com/AcreetionOS-Linux/argon2.git')
sha256sums=('SKIP')

pkgver() {
    cd "argon2"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


prepare() {
    cd "argon2"
    sed -i "/^#/d;s/\/@HOST_MULTIARCH@//g;s/@UPSTREAM_VER@/$pkgver/g" libargon2.pc.in
    sed -i '1d' libargon2.pc.in
}

build() {
    cd "argon2"
    make
}

check() {
    cd "argon2"
    make test
}

package() {
    cd "argon2"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0644 libargon2.pc.in $pkgdir/usr/lib/pkgconfig/libargon2.pc
}
